********************************************************************************************
*Programma di prova del dispositivo PIC
*Viene presentata una configurazione composta da un dispositivo M68000, un bus/memoria,
*un PIC e un generatore di interruzione/timer 1TO4INTGEN.
*L'indirizzo della ISR viene calcolato sommando lo spiazzamento di valore 0-7 a seconda delle linee di interruzione interrompenti 
*all'indirizzo base $40 che corrisponde al vettore 64 della tabella
*
*In questo esempio il bit AEOI è posto a 1: si noti che in questa modalità il simulatore 
*non mostra i bit del registro ISR e non ha il comportamento atteso.
*Gli studenti sono invitati a riferirsi al datasheet per studiare il corretto funzionamento.
*
********************************************************************************************	
*	NAME Prova PIC	;tale direttiva non e' allo stato supportata dall'assemblatore

	
*
ter	EQU	$2000	indirizzo base associato al dispositivo TERMINAL ter
intg1	EQU	$2004	indirizzo base associato al primo dispositivo 1TO4INTGEN
pic	EQU	$202C	indirizzo base associato al dispositivo PIC


*
*Area Programmi
*	
	ORG 	$A000	
begin

*codice di inizializzazione degli indirizzi base dei 4 device utilizzati
	
	MOVEA.W	#pic,A0			memorizza la base degli indirizzi dei device utilizzati 
	MOVEA.W	#intg1,A1		nei registri A0, A1, A3
	MOVEA.W #ter,A3

*scrittura nel registro TR: viene settato il valore $40 = 01000|000 che corrisponde al valore 64 ($40 in decimale), che indica
*il vettore di base (primo vettore utile per le ISR definite da utente). Tale vettore è memorizzato alla locazione 64*4=256=$100;
*i successivi accessi all' indirizzo dispari interesseranno il registro IMR 
	
	MOVE.B	#$40,1(A0)		

*inizializza il terminale: abilitazione eco e tastiera, pulisci buffer,canc video e disab. interrupt							
							 
	MOVE.B 	#%00111100,1(A3)	

*primo accesso in scrittura all'indirizzo pari: viene settato il registro CTRL ponendo AEOI=1 per il reset automatico 
*del bit di ISR dopo la trasmissione dell'interruzione 
*i prossimi accessi in lettura saranno al 
	*CTRL: se bit7=0, (è questo il caso)
	*IRR: se bit7=1 e bit6=0
	*ISR: se bit7=1 e bit6=1
    
	MOVE.B	#$10,(A0)	

*accesso al registro IMR: abilitazione di tutte le interruzioni							
	
	MOVE.B	#$00,1(A0)		

*Inizializzazione sistema: modifica dello SR in modo che nessuna interruzione sia mascherata
	MOVE.L	#$9000,A7		inizializza stack supervisor
	MOVE.W	SR,D0			legge il registro di stato
	ANDI.W	#$D8FF,D0 		maschera per reg stato (stato utente, int abilitati): 1101 1000 1111 1111
	MOVE.W	D0,SR			pone liv int a 000
	
*Inizializzazione registri timer 1	

	MOVE.B	#$11,4(A1)		inizializza pr=1; linea=1
	MOVE.B	#$01,5(A1)		inizializza vect#=1
	MOVE.B	#$1f,6(A1)		inizializza CountH
	MOVE.B	#$00,7(A1)		inizializza CountL

*Inizializzazione registri timer 2	

	MOVE.B	#$12,8(A1)		inizializza pr=1; linea=2		
	MOVE.B	#$02,9(A1)		inizializza vect#=2
	MOVE.B	#$1e,$a(A1)		inizializza CountH
	MOVE.B	#$00,$b(A1)		inizializza CountL

*Inizializzazione registri timer 3	

	MOVE.B	#$23,$c(A1)		inizializza pr=2; linea=3	
	MOVE.B	#$03,$d(A1)		inizializza vect#=3
	MOVE.B	#$1f,$e(A1)		inizializza CountH
	MOVE.B	#$00,$f(A1)		inizializza CountL

*Inizializzazione registri timer 4	

	MOVE.B	#$40,$10(A1)	inizializza pr=1; linea=0	
	MOVE.B	#$04,$11(A1)	inizializza vect#=4
	MOVE.B	#$1b,$12(A1)	inizializza CountH
	MOVE.B	#$00,$13(A1)	inizializza CountL

*Inizializzazione device 1to4intgen
	MOVE.B	#$57,(A1)		Intgen inizializza registro I con pr=5 e linea=7	
	MOVE.B	#$02,2(A1)		Intgen registro C lancia int sulla linea fisica indicata

loop	JMP	loop		<<<loop di attesa di int

*
*Area ISR per la gestione delle interruzioni generate da 1to4intgen
*Vengono definite 8 ISR che sono mappate in memoria a partire dall'indirizzo $100 e corrispondono
*alle ISR relative alle 8 linee di interruzione del PIC
*
* ISR associata alla linea 0
	ORG	$8000
int0	MOVE.B	#48,(A3)			stampa 0 a terminale
	NOP
	RTE

* ISR associata alla linea 1
	ORG	$8100
int1	MOVE.B	#49,(A3)			stampa 1 a terminale
	NOP
	RTE

* ISR associata alla linea 2
	ORG	$8200
int2	MOVE.B	#50,(A3)			stampa 2 a terminale
	NOP
	RTE

 * ISR associata alla linea 3
	ORG	$8300
int3	MOVE.B	#51,(A3)			stampa 3 a terminal
	NOP
	RTE

 * ISR associata alla linea 4
	ORG	$8400
int4	MOVE.B	#52,(A3)			stampa 4 a terminale
	NOP
	RTE
 
 * ISR associata alla linea 5
	ORG	$8500
int5	MOVE.B	#53,(A3)			stampa 5 a terminale
	NOP
	RTE


 * ISR associata alla linea 6
	ORG	$8600
int6	MOVE.B	#54,(A3)			stampa 6 a terminale
	NOP
	RTE


 * ISR associata alla linea 7
	ORG	$8700
int7	MOVE.B	#55,(A3)			stampa 7 a terminale
	NOP
	RTE
*

	end	begin
	
	
	
	





















































